function imageDisappeared searchMetrics
	set the strictProperties to false
	
	// if called with only an image path, turn it into a property list
	if searchMetrics is NOT a property list then put (image: searchMetrics) into searchMetrics
	
	put property waitFor of searchMetrics into timeoutValue
	if timeoutValue is empty then put 10.0 into timeoutValue
	put now + timeoutValue into endTime
	put 0 into searchMetrics.waitFor
	
	repeat until endTime
		if not imageFound(searchMetrics) then return true
	end repeat
	
	return false
end imageDisappeared


# Returns a rectangle representing the standard Windows 10 taskbar area.
-- Notes: System scale is set to 100% and large taskbar icons are enabled.
function taskbarRectangle
	put remoteScreenSize() into screenResolution
	put (0, screenResolution.height - 40) into topLeftAnchor // (0, 1040) for 1080p
	put screenResolution into bottomRightAnchor
	return (topLeftAnchor, bottomRightAnchor)
end taskbarRectangle